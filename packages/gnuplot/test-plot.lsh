;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; LUSH Lisp Universal Shell
;;;   Copyright (C) 2009 Leon Bottou, Yann LeCun, Ralf Juengling.
;;;   Copyright (C) 2002 Leon Bottou, Yann LeCun, AT&T Corp, NECI.
;;; Includes parts of TL3:
;;;   Copyright (C) 1987-1999 Leon Bottou and Neuristique.
;;; Includes selected parts of SN3.2:
;;;   Copyright (C) 1991-2001 AT&T Corp.
;;;
;;; This program is free software; you can redistribute it and/or modify
;;; it under the terms of the GNU Lesser General Public License as 
;;; published by the Free Software Foundation; either version 2.1 of the
;;; License, or (at your option) any later version.
;;;
;;; This program is distributed in the hope that it will be useful,
;;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU Lesser General Public License for more details.
;;;
;;; You should have received a copy of the GNU Lesser General Public
;;; License along with this program; if not, write to the Free Software
;;; Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, 
;;; MA 02110-1301  USA
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(libload "gnuplot/plot")

(defparameter *dtd2000*     (date-to-day (string-to-date "2000-1-1" 'year 'day)))
(defparameter *spd*         (* 24 3600))
(defparameter *finance.dat* (concat-fname lushdir "demos/gnuplot/finance.dat"))
(defparameter *world.dat*   (concat-fname lushdir "demos/gnuplot/world.dat"))
(defparameter *world.cor*   (concat-fname lushdir "demos/gnuplot/world.cor"))
(defparameter *optimize.dat* (concat-fname lushdir "demos/gnuplot/optimize.dat"))
(defparameter *arrowstyle.dat* (concat-fname lushdir "demos/gnuplot/arrowstyle.dat"))


;; read data in finance.dat
(defun read-finance-data ()
  (let ((m (double-array 500 12))
        (n 0) )
    (do ((line (lines *finance.dat*)))
      (let* ((ds (str-split line "\t"))
             ((mon day yr) (str-split (car ds) "/"))
             (date (string-to-date (sprintf "%s-%s-%s" yr mon day) 'year 'day))
             (row (idx-select m 0 n))
             (d0 (* (- (date-to-day date) *dtd2000*) *spd*)) )
        (row () (cons n (cons d0 (mapcar val (cdr ds)))))
        (incr n)))
    (idx-trim! m 0 0 n)
    m))


;; generic function
(defun read-xyz-data (filename)
  (let ((v ()) (m ()) (ms ()) 
	(lastline ()) (line ()) )
    (reading (open-read filename)
      ;; read lines
      (while (<> (skip-char "") "\e")
        (setq line (str-strip (read-string)))
        (read-string 1) ;; read newline character
        (cond 
         ;; ignore comment line
         ((str-startswith line "#") 
          ()) 
         
         ;; turn list of columns into a matrix
         ((and (= line "") (= lastline ""))
          (mapc idx-lift! m 1s)
          (setq ms (cons (apply mat-catcols (reverse m)) ms))
          (setq m ()) )
         
         ;; add another column-vector to m
         ((= line "")
          (mapc idx-lift! v 1s)
          (setq m (cons (apply mat-catcols (reverse v)) m))
          (setq v ()) )
	 
         ;; read line with xyz data
         (t
          (let ((i (index "#" line)))
            (when i (setq line (left line (- i 1))) ))
          (setq v (cons (reading-string (concat "[" line "]") (read)) v)) ))
        (setq lastline line)) )
    (if ms (reverse ms)
      (if m (reverse m)
        (mapc idx-lift! v 1s)
        (apply mat-catcols (reverse v)) ))))


;; partial translation of gnuplot/demo/finance.dem
(defun finance-demo ()
  (let* ((gp (new Gnuplot 'interactive ()))
         (m  (read-finance-data))
         (c05 (array-take m 1 [i 0 5]))
         (c02345 (array-take m 1 [i 0 2 3 4 5]))
         (c08  (array-take m 1 [i 0 8]))
         (c09  (array-take m 1 [i 0 9]))
         (c010 (array-take m 1 [i 0 10]))
         (c011 (array-take m 1 [i 0 11]))
         (g ()) )

    (setq g (plot* (title "Demo for plotting financial data")
                   (xrange 50 253)
                   (yrange 75 105)
                   (ytics [105 100 95 90 85 80])
                   (margin 'left 0.15  'right 0.95)
                   (lines c05)
                   ))
    (==> gp do-graph g)

    (pause "Turn on grid")
    (==> g add-preamble "set grid")
    (==> g add-preamble "set title 'Turn on grid'")
    (==> gp do-graph g)
    
    (pause "Finance Bars")
    (setq g (plot* (title "Finance bars")
                   (xrange 50 253)
                   (yrange 75 105)
                   (ytics [105 100 95 90 85 80])
                   (margin 'left 0.15  'right 0.95)
                   (grid)
                   "set logscale y"
                   (financebars c02345 (lt 8))
                   ))
    (gp "reset")
    (==> gp do-graph g)
    
    (pause "Bolling Bands")
    (setq g (plot* (title "Bollinger Bands")
                   (xrange 50 253)
                   (yrange 75 105)
                   (ytics [105 100 95 90 85 80])
                   (margin 'left 0.15  'right 0.95)
                   (grid)
                   "set logscale y"
                   (linewidth 2)
                   (financebars c02345 (lc "black") (lw 1))
                   (lines c09  (lc "orange"))
                   (lines c010 (lc "blue"))
                   (lines c011 (lc "red"))
                   ))
    (gp "reset")
    (==> gp do-graph g)
    
    (pause "With indicator on separate scale")
    (==> g add-preamble "set title 'Indicator on separate scale'")
    (==> g add-line "lines" "lc rgb \"purple\" axes x1y2" c08)
    (gp "reset")
    (==> gp do-graph g)
    gp))


;; partial translation of gnuplot/demo/circles.dem
(defun circles-demo ()
  (let* ((world (read-xyz-data *world.dat*))
         (coord (read-xyz-data *world.cor*))
         (optim (car (read-xyz-data *optimize.dat*)))
         (optcs (idx-extend optim 1 -1))
         (plotter (new Gnuplot 'interactive ())) )
    (let ((rs (double-array (length coord))))
      (idx-bloop ((r rs)) (r (+ 2 (* 3 (rand)))))
      (setq coord (mat-catrows coord ($> rs))) )

    (when (> *gnuplot-version* 4.2)
      (plot (title "")
            (xlabel "Note that overlapping transparent circles produce a darker area")
            "set style fill solid 1.0 border -1"
            "set obj 20 rect from graph 0,0 to graph 1,1 behind fc rgb \"cyan\" fs solid 0.2"
            "set border"
            notics
            (yrange -70 *)
            (add-arc   0  20  -140 -40 30  (fc 'red))
            (add-arc  20  50  -140 -40 30  (fc 'orange))
            (add-arc  50  90  -140 -40 30  (fc 'yellow))
            (add-arc  90 120  -140 -40 30  (fc 'forest-green))
            (add-arc 120 190  -140 -40 30  (fc 'dark-turquoise))
            (add-arc 190 360  -140 -40 30  (fc 'dark-magenta))
            (filledcurves world (lc 'light-green))
            (circles coord (fill 0.5) (bc ())) )
      (pause)
      (plotter "reset")
    
      (plot (title "Trace of unconstrained optimization with trust-region method")
            "set size ratio -1"
            (xrange -2.5 1.5)
            (yrange -1.0 2.5)
            (circles optim (lc 'blue) (fill 0.14) (bc ()))
            (linespoints optcs (lw 2) (lc 'black)) )
      (pause)
      (plotter "reset") 
      ) ; gnuplot version > 4.2

    ;; this part requires OpenCV
    (libload "libimage/image-io")
    (libload "opencv/color")
    (libload "opencv/keypoints")
    (let* ((img (image-read "lena.jpg"))
           (gimg (cv-rgb2gray img))
           (xys (cv-keypoints gimg 20 0.05)) )

      ;; points instead of circles, 'cause circles need a 3rd column
      (plot (title "Lena's key points")
            "set yrange [*:*] reverse"
            notics
            (xy-aspect 1)
            (image img)
            (points xys (lc 'yellow) (pt 6) (ps 2))
            )
      (pause)
      (plotter "reset")

      ;; scale y-axis differently 
      (plot (title "Different sampling intervals for X and Y-directions")
            "set palette gray"
            "unset colorbox"
            "set tics out"
            (xrange -0.5 (* ($ img 1) 2))
            (yrange ($ img 0) -0.5)     ; y range reversed
            (xy-aspect 1)
            (image gimg (dx 2) (dy 1))
            (points (* xys [2 1]) (lc 'yellow) (pt 6) (ps 2))
            )
      )
    ))

(defun object-demo ()
  (let* ((world (read-xyz-data *world.dat*))
         (coord (read-xyz-data *world.cor*))
         (vdat ())
         (adat ())
         (plotter (new Gnuplot 'interactive ())) )
    (let ((offs (clone-array coord)))
      (idx-bloop ((off offs))
        (let ((phi (rand 0 +2pi+)))
          (off 0 (* 6 (cos phi)))
          (off 1 (* 6 (sin phi))) ))
      (setq adat (mat-catrows (- coord offs) coord))
      (setq vdat (mat-catrows (- coord offs) offs)) )

    ;; first way: use plotting style 'vectors'
    (plot (title "Locations marked with vectors")
          "set style fill solid 1.0 border -1"
          "set obj 20 rect from graph 0,0 to graph 1,1 behind fc rgb \"cyan\" fs solid 0.2"
          "set border"
          notics
          (yrange -70 *)
          (filledcurves world (lc 'light-green))
          (vectors vdat (lc 'brown) (lw 2)) )
    (pause)
    (plotter "reset")
    
    ;; second way: add arrows
    (plot (title "Locations marked with arrows")
          "set style fill solid 1.0 border -1"
          "set obj 20 rect from graph 0,0 to graph 1,1 behind fc rgb \"cyan\" fs solid 0.2"
          "set border"
          notics
          (yrange -70 *)
          (filledcurves world (lc 'light-green))
          (do ((d vdat))
            (add-arrow (d 0) (d 1) (d 2) (d 3) (lc 'brown) (lw 2)) ))
    (pause)
    (plotter "reset")

    (plot (title "Locations marked with arrows*")
          "set style fill solid 1.0 border -1"
          "set obj 20 rect from graph 0,0 to graph 1,1 behind fc rgb \"cyan\" fs solid 0.2"
          "set border"
          notics
          (yrange -70 *)
          (filledcurves world (lc 'light-green))
          (do ((d adat))
            (add-arrow* (d 0) (d 1) (d 2) (d 3) (lc 'brown) (lw 2)) ))
    (pause)
    (plotter "reset")

    ;; good old sine-curve
    (let ((x (arange (- +pi+) +2pi+ 0.01)))
      (plot (title "Sine")
            "set termoption enhanced"
            (add-vline 0 (zp 'back))
            (add-hline 0 (lt 0) (zp 'back))
            (add-vline +pi+ (lt 0))
            (add-label (+ +pi+ 0.2) 0.1 "Zero-crossing at x={/Symbol p}")
            (xrange -1 6.2)
            (yrange -1.2 1.2)
            (lines (++> x (sin x)) (lw 1.5)) ))
    ))
                   
